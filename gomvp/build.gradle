apply plugin: 'com.android.library'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 26



    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            java {
                srcDirs 'src/java'
            }
        }
    }
}

version = "1.3.0"

def siteUrl = 'https://github.com/wuchengithub/GoMVP'      // 项目的主页
def gitUrl = 'https://github.com/wuchengithub/GoMVP.git'   // Git仓库的url
group = "com.wookii.gomvp"
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'MVP for Android.'
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'wuchengithub'     //填写的一些基本信息
                        name 'chenwu'
                        email 'wuchenxy2015@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from 'src/java'
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = 'src/java'
//    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "wookii"
        name = "GoMVP"   //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation Libs.support_v4
    implementation Libs.support_v7
    api Libs.okhttp
    api Libs.okhttp_logging
    api Libs.retrofit
    api Libs.retrofit_gson
    api Libs.retrofit_rxjava
    api Libs.rxandroid
    api Libs.rxjava2
    api Libs.gson
    implementation Libs.support_annotation
    implementation Libs.butternife
    kapt Libs.butternife_processor
    implementation Libs.aspectJ


}
